/*
Loot That Auto
You can now loot every vehicle ingame

Current: v1.0 (30/07/2024)

Plans for v1.1
- If you loot police vehicles, you will find weapons/ammo...
- If you loot gang vehicles, you will find more money than usual...

LOOT THAT AUTO FOR GTA 3 AND SAN ANDREAS COMING SOON...
*/
{$CLEO .cs}
0000: 

const
    LOOTEADO = 0@ //0 = cant loot now 1 = can loot now
    BUSCAR = 1@ // 3/10 chances of finding
    DINEROLOOT = 2@ // The amount of money found on the vehicle
    CARRO = 3@ // actual vehicle
    LASTCARRO = 4@ // last vehicle you drived
    ISSAMECAR = 8@ // 0 = the actual car is different than the last one 1 = they are the same car
end

//ISSAMECAR = 0
LOOTEADO = 0


04C4: create_coordinate 5@ 6@ 7@ from_actor $PLAYER_ACTOR offset 0.0 0.0 0.0
05F0: LASTCARRO = random_vehicle_near_point 5@ 6@ 7@ in_radius 15.0 find_next 0 pass_wrecked 0 //IF and SET
/*
THOSE 2 LINES UP HERE: LAST VEHICLE DRIVED IS FIRSTLY GOING TO BE A RANDOM VEHICLE, 
THIS NORMALLY MEANS THAT YOU CANT LOOT THE CLOSEST VEHICLE 
TO THE PLAYER WHEN LOADING A SAVEFILE... NORMALLY xD 
*/
 
// CHECKCAR: CHECK CONSTANTLY IF ITS IN A CAR 
// - IF ITS IN A CAR, CHECK IF ITS THE SAME CAR OR NOT
// - IF ITS NOT, SAVE THE VEHICLE AND GO STRAIGHT AND LOOT THE VEHICLE
// - ONCE SAVED, YOU CANT LOOT THE EXACT SAME CAR (you will eventually be able to...)
:checkcar 
wait 0 
if
    00DF:   actor $PLAYER_ACTOR in_any_car
then      
    03C0: CARRO = actor $PLAYER_ACTOR car_no_save
    if
       8039: not CARRO == LASTCARRO   
    then
        ISSAMECAR = 0
        0085: LASTCARRO = CARRO
        jump @lootcar
    else
        //0ACA: "It is the same vehicle."
        ISSAMECAR = 1
        jump @checkcar
    end
else
    jump @checkcar
end

/*
LOOTCAR: ONCE CHECKCAR HAS ALL REQUIREMENTS, YOU CAN TRY AND LOOT THE VEHICLE
- YOU WILL NEED TO CHECK AGAIN IF PLAYERS IS STILL ON A VEHICLE
- YOU CAN ONLY LOOT ONCE UNTIL YOU CHANGE VEHICLES
- LIKE I SAID BEFORE, YOU CAN ACTUALLY LOOT THE SAME CAR BUT 
  ONLY IF YOU GRAB ANOTHER ONE AND GET BACK TO GRAB THE FIRST ONE YOU DRIVED
- THERE IS A 10s COOLDOWN AFTER LOOTING SUCCESFULLY/UNSUCCESFULLY A VEHICLE
*/
:lootcar
wait 0
if and
    00DF:   actor $PLAYER_ACTOR in_any_car  
    LOOTEADO == 0
    ISSAMECAR == 0
then
    wait 0            
    LOOTEADO = 1
    0209: BUSCAR = random_int_in_ranges 0 11
    if
        BUSCAR > 7
    then
        wait 1000
        0209: DINEROLOOT = random_int_in_ranges 5 30 
        0ACE: show_formatted_text_box "You found $%d in the vehicle..." DINEROLOOT 
        0109: player $PLAYER_CHAR money += DINEROLOOT
    else
        //0ACA: "You didnt find shit..."                 
    end
    wait 10000 // cooldown
    LOOTEADO = 0
    //0ACA: "can loot now"
    jump @checkcar
else
    jump @checkcar       
end    





 



